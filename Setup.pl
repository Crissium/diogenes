#!/usr/bin/perl -w
# This is the installation script for the Diogenes Daemon.  It
# generates a configuration file located in the Diogenes
# directory, called .diogenesrc under Unix and diogenes.ini under
# Windows.  This file overrides settings in the other Unix config files:
# /etc/diogenesrc and ~/.diogenesrc.

# Users may have to edit the configuration file by hand.

use strict;
require 5.005;
use Cwd;
use Diogenes;
my $cwd = cwd;

my @required_files = qw(Diogenes.pm Daemon.pl Diogenes.cgi);
for (@required_files)
{
	unless (-e "$cwd/$_")
	{
		print "It appears that $_, which is a necessary file for the \n",
			"proper functioning of Diogenes, is not present in the \n",
			"current folder.  Try unzipping the package again.\n",
			"Press enter to exit.\n";
		<>;
		exit;
	}
}

my $win = ($^O=~/MSWin/i or $^O =~/dos/);
my $osx = $^O=~/darwin/i;
my $unix = not $win;
my $rc_file = $unix ? '.diogenesrc' : 'diogenes.ini';
my $init = new Diogenes(-type => 'none');
my $old_file;

if (-e "$cwd/$rc_file")
{
	my $suffix = "001";
	$suffix++ while -e "$cwd/$rc_file.$suffix";
	
	print "There is a configuration file ($rc_file) already present\n",
		  "in the current working directory.\n\n",
		  
		  "The old file is going to be renamed as $rc_file.$suffix\n",
		  "Press enter to continue.\n";
		  <>;
	rename "$cwd/$rc_file", "$cwd/$rc_file.$suffix" or die "Couldn't rename file: $!";
	unlink "$cwd/$rc_file";
	$old_file = 'true';
}


print << "END";

This is the installation script for Diogenes.  It simply writes some
configuration information to a file called $rc_file in this folder.

END
open INI, ">$rc_file" or die "Error: I could not create the file $rc_file!\n";

my %old_config;
$old_config{phi_file_prefix} = 'lat';
my @critical = qw/tlg_dir phi_dir ddp_dir phi_file_prefix/;
for (@critical)
{
	$old_config{$_} = qq{""};
	$old_config{$_} = $init->{$_} if $init->{$_};
}
	
if ($unix and (not -e '/etc/diogenesrc' and not -e "$ENV{HOME}/.diogenesrc"
								 and not $old_file))
{	# No old config to work from
	if ($osx)
	{
		$old_config{tlg_dir} = '/Volumes/TLG_E';
		$old_config{phi_dir} = '/Volumes/Phi5';
		$old_config{ddp_dir} = '/Volumes/Phi7';
	}
	else
	{	
        	my $fallback = '/mnt/cdrom';
		for my $type qw(tlg ddp phi)
		{
			my $guess = `locate authtab.dir | grep $type`;
			$guess =~ s#[^/]*$##;
        		$guess = $fallback unless $guess =~ /\S/;
			my $dir = $type.'_dir';
			$old_config{$dir} = $guess;
		}
	}
}

else
{	# Windows
		
	if (not $old_file)
	{
		print << "END";
I will now look for the location of the databases.  Please put in a
PHI, TLG, or DDP CD-ROM and press enter.  If you do not have a disk to
hand, just press enter.
END
		<>;

		if ($win)
		{
			my $drive;
			# Figure out where the CD-Rom is.
			$drive = 'c';  # Start with c: on first pass
			$drive++ until -e "$drive:\\authtab.dir" or $drive eq 'aa';
			if ($drive eq 'aa')
			{
				print "I could not find any CD-ROM in the system.\n",
					"You will have to re-run this program later, or enter\n",
					"the location(s) of the databases by hand in the\n",
					"diogenes.ini file.  For now I will say that the databases\n",
					"are to be found in your \"D\" drive\n\n";
				$drive = "D";
			}
			# Note lack of quotes, so as to not escape the final one with the backslash.
			print "Using disk in drive $drive\n";
			$old_config{$_} = qq{$drive:\\} for grep /dir/, @critical;
		}

	}
}

my $enable_latex = not ($win or $osx);

print INI << "END";

# This is the configuration file for Diogenes, generated by the Setup
# program.  You may have to change these values to fit your system.

# NB.  If you have changed any values by using the "Settings" feature of
# the web page, those values will be appended to the end of this file.
# Later values override those set earlier in the file.

# For settings that are either on or off, use a value of 1 for on 
# and 0 for off.

# Comments begin with `#' and blank lines are ignored. Each
# keyword/value pair must be on a separate line.  If a value contains
# spaces, enclose it in double quotes ("").  Keyword and value may
# optionally be separated by an equals sign.

# The first three lines are crucial -- they tell the program
# where to find your CD-Rom.

END

print INI "tlg_dir \"$old_config{tlg_dir}\"\n";
print INI "phi_dir \"$old_config{phi_dir}\"\n";
print INI "ddp_dir \"$old_config{ddp_dir}\"\n";

print INI << "END";

# This is the prefix with which the files on the PHI Latin disk begin.
# It usually is "LAT", but some older disks have "PHI" instead.  If you
# have one of these older disks, you will have to change this entry.

phi_file_prefix "$old_config{phi_file_prefix}"

# Here you may specify which database is selected by default when you
# first open the Diogenes web page.  Other values might be "TLG Texts"
# or "TLG Word List", etc.

# cgi_default_corpus "TLG Texts"
cgi_default_corpus "PHI Latin Corpus"

# Here you may choose the Greek encoding to be used by default, when you
# first are presented with a page of Greek.  You may still of course
# change this to any of the other encodings.

cgi_default_encoding UTF-8

# Here is the list of Unicode fonts you have on your system, in order of
# preference.  It doesn't matter if you don't have any of these -- this
# is just used to give a hint to your browser.  

unicode_font  "Arial Unicode MS,TITUS Cyberbit Basic,Porson,Athena"

# Here is the number chunks of text the text browser displays per
# screen.  A normal value is 1, but you can use a higher number in
# conjunction with the browse_lines parameter in order to display a
# large amount of text with citation information every few lines.

browser_multiple 1

# This is the size of each chunk of text in lines.

browse_lines 39

# For more frequent citation information, try:
#browser_multiple 10
#browse_lines 4

# If you wish, you may specify a file with the code numbers of texts you
# never want to search ...

# blacklist_file  "$cwd/blacklist"

# You may change the input format to "BETA" if you prefer to enter raw
# Beta code, rather than Perseus-style transliteration.

#cgi_input_format BETA
cgi_input_format Perseus

# Specify the nearest mirror of the Perseus website for quickest response:
perseus_server = http://www.perseus.tufts.edu/
#perseus_server = http://perseus.csad.ox.ac.uk/
#perseus_server = http://perseus.mpiwg-berlin.mpg.de/

# Should the TLG text selection page default to texts meeting "any" or 
# "all" criteria? 
cgi_default_criteria "Any"

# Present bibliographical info at the first citation of each work (this
# requires a substantial amount of memory)
bib_info 1

# Give HTML links to the Perseus morphological analysis tools by
# default?
perseus_links 1

# Use the index to the TLG word list, which is keyed by the first two
# letters of the word.  If you use this, word list searches are quicker,
# but less comprehensive (eg. agathos does not find KA)GAQO/S).
use_tlgwlinx 0

# If you are using an old, slow computer without a lot of memory, it
# might help to uncomment the following three lines:
#bib_info 0
#use_tlgwlinx 1
#perseus_links 0



########################################################################

# Here are some settings that pertain to the running of the Diogenes Daemon
# in the present directory ($cwd).

cgi_root_dir "$cwd/"
cgi_tmp_dir "$cwd/cache"
cgi_img_dir_absolute = "$cwd/images/"
cgi_img_dir_relative = /images/
cgi_prefix Diogenes.
cgi_check_mod_perl = 0
dump_file "$cwd/dump_file.txt"

# This is to enable the choice of displaying pretty images of Greek via
# LaTeX -- it should not be enabled, unless you have the necessary
# software installed.

cgi_enable_latex = $enable_latex

END

if ($unix)
{
	my $cgi_gs = `which gs`;
	my $cgi_p2g = `which ppmtogif`;
	my $cgi_latex = `which latex`;
	my $cgi_ps2pdf = `which ps2pdf`;
	my $cgi_dvips = `which dvips`;     
	chomp ($cgi_gs, $cgi_p2g, $cgi_latex, $cgi_ps2pdf, $cgi_dvips);
	print INI <<"END";
########################################################################
# Here are some options that are only likely to be be of interest on a
# unix-like platform

# If you have version 4 of the Ibycus font, and the Hephaistio prosody
# font.  

ibycus4 1 
prosody 1

# For pdf generation: for best results you should have the Type 1 versions
# of Ibycus and Computer Modern installed.  If you get jagged, bitmapped
# fonts in your pdf files despite setting this to a true value, upgrade
# to a more recent version of ps2pdf.
psibycus 1

# If you want the words in the TLG wordlist to be displayed as tiny GIFs
cgi_latex_word_list 0

# LaTeX configuration
cgi_gs = "$cgi_gs"
cgi_p2g = "$cgi_p2g"
cgi_latex = "$cgi_latex"
cgi_ps2pdf = "$cgi_ps2pdf"
cgi_dvips = "$cgi_dvips"       

# latex_pointsize 10
# latex_baseskip 12

# Some people like to mount their CD-Rom using uppercase filenames
# uppercase_files 1

# Note that this file is designed for running the Diogenes Daemon
# as a non-privileged user.  If you wan to run Diogenes under Apache,
# see the file diogenesrc.apache that came with this software.
END
}
print INI '
######################## End of Configuration File ######################';

close INI or die "Unable to close diogenes.ini: $!";

print << "END";

The $rc_file file has been created successfully in the current folder
($cwd).  
This file contains your preferences for using Diogenes.

You may wish to look at this file later to examine the configuration
options available to you.  If you wish to change any of your choices,
you may edit that file, or you may rename or delete the file and run
Setup again and a new file will be generated.

                           * * *

Now you may click on the Daemon file to start Diogenes.

Press Enter to exit.

END
<>;
exit;
